import builder.html
import idoc.html (adjust_headings)
import markdown
import std.clone (Clone)
import std.io (Error as IoError, Read)
import std.json
import wobsite.markdown (SyntaxHighlight)

# A Markdown document.
type Markdown {
  let @inner: markdown.Document

  fn static new -> Markdown {
    Markdown(markdown.Document.new)
  }

  fn static parse[T: mut + Read[IoError]](
    parser: mut json.PullParser[T, IoError],
  ) -> Result[Markdown, json.Error[IoError]] {
    let start = try parser.start_of_next_value

    parser.string.then(fn (md) {
      markdown.Document.parse(md).map(fn (v) { Markdown(v) }).map_error(
        fn (e) { json.Error.generic(e.message, start + e.offset) },
      )
    })
  }

  fn empty? -> Bool {
    @inner.children.empty?
  }

  fn to_html(increment_headings: Int) -> html.Document {
    let doc = @inner.to_html

    SyntaxHighlight.new.run(doc)

    if increment_headings > 0 { adjust_headings(doc, increment_headings) }

    doc
  }

  fn summary -> Markdown {
    let doc = markdown.Document.new

    match @inner.children.get(0) {
      case Ok(v) -> doc.children.push(v.clone)
      case _ -> {}
    }

    Markdown(doc)
  }
}

impl Clone for Markdown {
  fn pub clone -> Markdown {
    Markdown(@inner.clone)
  }
}
